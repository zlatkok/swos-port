//
// Video options menu
//

#include "common.mh"

Menu videoOptionsMenu
{
    kGamePlayOptionsStartY = kOptionsStartY - kOptionHeight

    onInit: videoOptionsMenuOnInit

    initialEntry: flashCursor

    defaultWidth: kOptionWidth
    defaultHeight: kOptionHeight

    Entry header {
        x: kBaseX
        y: 0
        width: kBigButtonWidth
        height: kBigButtonHeight
        text: "VIDEO OPTIONS"
        color: kHeaderColor
    }

    defaultX: kOptionsColumn1X
    defaultY: @prevEndY + kOptionsRowDistance

    TemplateEntry {
        color: kOptionsColumn1Color
    }

    Entry linearFilteringLabel {
        y: kGamePlayOptionsStartY
        text: "LINEAR FILTERING"
    }

    Entry flashMenuCursorLabel { text: "FLASH MENU CURSOR" }
    Entry showTouchTrailsLabel { text: "SHOW TOUCH TRAILS" }
    Entry transparentVirtualJoypadButtonsLabel { text: "BLEND VIRTUAL JOYPAD" }

    entryAlias firstLabel = linearFilteringLabel

    defaultX: kOptionsColumn2X

    TemplateEntry {
        color: kOptionsColumn2Color
    }

    Entry linearFiltering {
        y: kGamePlayOptionsStartY
        downEntry: >

        stringTable: [ ~m_useLinearFiltering, swos.aOff, swos.aOn ]
        onSelect: switchLinearFiltering
    }

    Entry flashCursor {
        upEntry: <
        downEntry: >

        stringTable: [ ~m_flashCursor, swos.aOff, swos.aOn ]
        onSelect: changeFlashCursor
    }

    Entry showTouchTrails {
        upEntry: <
        downEntry: >

        stringTable: [ ~m_showTouchTrails, swos.aOff, swos.aOn ]
        onSelect: changeShowTouchTrails
    }

    Entry transparentVirtualJoypadButtons {
        upEntry: <
        downEntry: >

        stringTable: [ ~m_transparentButtons, swos.aOff, swos.aOn ]
        onSelect: changeVirtualJoypadTransparentButtons
    }

    entryAlias firstChangerItem = linearFiltering

    Entry showWindowMode {
        x: (@kScreenWidth - kOptionWidth) / 2 - kMenuOffset
        y: @prevEndY + 3 * kOptionsRowDistance

        upEntry: <
        downEntry: >

        color: @kGreen
        text: "WINDOW MODE"
        onSelect: openWindowModeMenu
    }

    Entry exit {
        x: kBaseX
        y: kExitY
        width: kBigButtonWidth
        height: kBigButtonHeight

        color: kExitColor
        textFlags: 0
        text: swos.aExit

        upEntry: <
        onSelect: ~SetExitMenuFlag
    }
}
