//
// Video options menu
//

#include "common.mh"

Menu videoOptionsMenu
{
    kGameplayOptionsStartY = kOptionsStartY - kOptionHeight

    onInit: videoOptionsMenuOnInit
    onRestore: videoOptionsMenuOnInit

    initialEntry: flashCursor

    defaultWidth: kOptionWidth
    defaultHeight: kOptionHeight

    Entry header {
        x: kBaseX
        y: 0
        width: kBigButtonWidth
        height: kBigButtonHeight
        text: "VIDEO OPTIONS"
        color: kHeaderColor
    }

    defaultX: kOptionsColumn1X
    defaultY: @prevEndY + kOptionsRowDistance

    TemplateEntry {
        color: kOptionsColumn1Color
    }

    Entry zoomLabel { y: kGameplayOptionsStartY  text: "ZOOM FACTOR" }
    Entry linearFilteringLabel                 { text: "LINEAR FILTERING" }
    Entry flashMenuCursorLabel                 { text: "FLASH MENU CURSOR" }
    Entry fancyItemBackgroundLabel             { text: "MENU GRADIENT BACKGROUND" }
    Entry showFpsLabel                         { text: "SHOW FPS INDICATOR" }
    Entry clearScreenLabel                     { text: "CLEAR SCREEN" }
    Entry showTouchTrailsLabel                 { text: "SHOW TOUCH TRAILS" }
    Entry transparentVirtualJoypadButtonsLabel { text: "BLEND VIRTUAL JOYPAD" }

    defaultX: kOptionsColumn2X

    TemplateEntry {
        color: kOptionsColumn2Color
    }

    Entry decreaseZoomButton {
        y: kGameplayOptionsStartY
        width: kChangerWidth

        color: kChangerColor
        text: "-"

        rightEntry: zoom
        downEntry: linearFiltering

        onSelect: decreaseZoom
    }

    Entry zoom {
        x: @prevEndX + 1
        y: kGameplayOptionsStartY
        width: kChangerOptionWidth

        text: @kAlloc

        leftEntry: decreaseZoomButton
        rightEntry: increaseZoomButton
        downEntry: linearFiltering

        onSelect: enterZoom
        beforeDraw: zoomBeforeDraw
    }

    Entry increaseZoomButton {
        x: @prevEndX + 1
        y: kGameplayOptionsStartY
        width: kChangerWidth

        color: kChangerColor
        text: "+"

        leftEntry: zoom
        downEntry: linearFiltering

        onSelect: increaseZoom
    }

    Entry linearFiltering {
        boolOption: getLinearFiltering, setLinearFiltering, "linear filtering"

        upEntry: zoom
        downEntry: >
    }

    Entry flashCursor {
        boolOption: cursorFlashingEnabled, setFlashMenuCursor, "cursor flashing"

        upEntry: <
        downEntry: >
    }

    Entry fancyItemBackground {
        boolOption: menuGradientFillEnabled, enableMenuGradientFill, "menu background gradient"

        upEntry: <
        downEntry: >
    }

    Entry showFps {
        boolOption: getShowFps, setShowFps, "FPS indicator"

        upEntry: <
        downEntry: >
    }

    Entry clearScreen {
        boolOption: getClearScreen, setClearScreen, "clear screen"

        upEntry: <
        downEntry: >
    }

    Entry showTouchTrails {
        text: @kAlloc

        upEntry: <
        downEntry: >

        beforeDraw: setTouchTrailsEntryText
        onSelect: changeShowTouchTrails
    }

    Entry transparentVirtualJoypadButtons {
        text: @kAlloc

        upEntry: <
        downEntry: >

        beforeDraw: setTransparentButtonsEntryText
        onSelect: changeVirtualJoypadTransparentButtons
    }

    Entry showWindowMode {
        x: (@kScreenWidth - kOptionWidth) / 2 - kMenuOffset
        y: @prevEndY + 3 * kOptionsRowDistance

        color: @kGreen
        text: "WINDOW MODE"

        upEntry: <
        downEntry: >

        onSelect: openWindowModeMenu
    }

    Entry exit {
        x: kBaseX
        y: kExitY
        width: kBigButtonWidth
        height: kBigButtonHeight

        color: kExitColor
        textFlags: 0
        text: swos.aExit

        upEntry: <

        onSelect: ~SetExitMenuFlag
    }
}
